plugins {
    id 'java'
    id 'groovy'
    id 'jacoco'
}

group 'org.example'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'junit', name: 'junit', version: '4.12'

    implementation 'org.codehaus.groovy:groovy-all:2.4.11'
    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:3.+"
    testImplementation 'org.codehaus.groovy:groovy-all:2.4.11'
    testImplementation 'org.spockframework:spock-core:1.1-groovy-2.4'



    // Java SDK MercadoPago lib
    implementation 'com.mercadopago:sdk-java:1.9.0'

    //Spark Rest API lib
    implementation 'com.sparkjava:spark-core:2.9.3'


    implementation "org.slf4j:slf4j-simple:1.6.1"

}

test {
    testLogging {
        showStandardStreams = false
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

check.dependsOn jacocoTestReport

// Task to open the report generated by Jacoco
task openReport {
    doLast {
        exec {
            executable 'open'
            args 'build/reports/jacoco/test/html/index.html'
        }
    }
}

// Task to clean the directory, run tests, generate the Jacoco report and open it
task testAndReport {
    dependsOn 'clean'
    dependsOn 'test'
    dependsOn 'jacocoTestReport'
    dependsOn 'openReport'
    tasks.findByName('test').mustRunAfter 'clean'
    tasks.findByName('jacocoTestReport').mustRunAfter 'test'
    tasks.findByName('openReport').mustRunAfter 'jacocoTestReport'
}

//To run tests with java classes using groovy classes is necessary compile first groovy files and then java files
//You should remove this config if groovy classes were removed
sourceSets {
    main {
        java {
            srcDirs = [] // don't compile Java code twice
        }
        groovy {
            srcDirs = [ 'src/main/groovy', 'src/main/java' ]
        }
    }
}
